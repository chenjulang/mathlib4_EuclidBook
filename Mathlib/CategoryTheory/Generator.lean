/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import Mathlib.CategoryTheory.Limits.EssentiallySmall
import Mathlib.CategoryTheory.Limits.Opposites
import Mathlib.CategoryTheory.Subobject.Lattice
import Mathlib.Data.Set.Opposite

/-!
# Separating and detecting sets

There are several non-equivalent notions of a generator of a category. Here, we consider two of
them:

* We say that `ùí¢` is a separating set if the functors `C(G, -)` for `G ‚àà ùí¢` are collectively
    faithful, i.e., if `h ‚â´ f = h ‚â´ g` for all `h` with domain in `ùí¢` implies `f = g`.
* We say that `ùí¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ùí¢` uniquely factors through `f`, then `f` is an isomorphism.

There are, of course, also the dual notions of coseparating and codetecting sets.

## Main results

We
* define predicates `IsSeparating`, `IsCoseparating`, `IsDetecting` and `IsCodetecting` on
  sets of objects;
* show that separating and coseparating are dual notions;
* show that detecting and codetecting are dual notions;
* show that if `C` has equalizers, then detecting implies separating;
* show that if `C` has coequalizers, then codetecting implies separating;
* show that if `C` is balanced, then separating implies detecting and coseparating implies
  codetecting;
* show that `‚àÖ` is separating if and only if `‚àÖ` is coseparating if and only if `C` is thin;
* show that `‚àÖ` is detecting if and only if `‚àÖ` is codetecting if and only if `C` is a groupoid;
* define predicates `IsSeparator`, `IsCoseparator`, `IsDetector` and `IsCodetector` as the
  singleton counterparts to the definitions for sets above and restate the above results in this
  situation;
* show that `G` is a separator if and only if `coyoneda.obj (op G)` is faithful (and the dual);
* show that `G` is a detector if and only if `coyoneda.obj (op G)` reflects isomorphisms (and the
  dual).

## Future work

* We currently don't have any examples yet.
* We will want typeclasses `HasSeparator C` and similar.

-/


universe w v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

open CategoryTheory.Limits Opposite

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  (E : Type u‚ÇÉ) [Category.{v‚ÇÉ} E]

/-- We say that `ùí¢` is a separating set if the functors `C(G, -)` for `G ‚àà ùí¢` are collectively
    faithful, i.e., if `h ‚â´ f = h ‚â´ g` for all `h` with domain in `ùí¢` implies `f = g`. -/
def IsSeparating (ùí¢ : Set C) : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ G ‚àà ùí¢, ‚àÄ (h : G ‚ü∂ X), h ‚â´ f = h ‚â´ g) ‚Üí f = g

/-- We say that `ùí¢` is a coseparating set if the functors `C(-, G)` for `G ‚àà ùí¢` are collectively
    faithful, i.e., if `f ‚â´ h = g ‚â´ h` for all `h` with codomain in `ùí¢` implies `f = g`. -/
def IsCoseparating (ùí¢ : Set C) : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ G ‚àà ùí¢, ‚àÄ (h : Y ‚ü∂ G), f ‚â´ h = g ‚â´ h) ‚Üí f = g

/-- We say that `ùí¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ùí¢` uniquely factors through `f`, then `f` is an isomorphism. -/
def IsDetecting (ùí¢ : Set C) : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ G ‚àà ùí¢, ‚àÄ (h : G ‚ü∂ Y), ‚àÉ! h' : G ‚ü∂ X, h' ‚â´ f = h) ‚Üí IsIso f

/-- We say that `ùí¢` is a codetecting set if the functors `C(-, G)` collectively reflect
    isomorphisms, i.e., if any `h` with codomain in `G` uniquely factors through `f`, then `f` is
    an isomorphism. -/
def IsCodetecting (ùí¢ : Set C) : Prop :=
  ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ G ‚àà ùí¢, ‚àÄ (h : X ‚ü∂ G), ‚àÉ! h' : Y ‚ü∂ G, f ‚â´ h' = h) ‚Üí IsIso f

section Dual

theorem isSeparating_op_iff (ùí¢ : Set C) : IsSeparating ùí¢.op ‚Üî IsCoseparating ùí¢ := by
  refine ‚ü®fun hùí¢ X Y f g hfg => ?_, fun hùí¢ X Y f g hfg => ?_‚ü©
  ¬∑ refine Quiver.Hom.op_inj (hùí¢ _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ (Set.mem_op.1 hG) _
  ¬∑ refine Quiver.Hom.unop_inj (hùí¢ _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ (Set.op_mem_op.2 hG) _

theorem isCoseparating_op_iff (ùí¢ : Set C) : IsCoseparating ùí¢.op ‚Üî IsSeparating ùí¢ := by
  refine ‚ü®fun hùí¢ X Y f g hfg => ?_, fun hùí¢ X Y f g hfg => ?_‚ü©
  ¬∑ refine Quiver.Hom.op_inj (hùí¢ _ _ fun G hG h => Quiver.Hom.unop_inj ?_)
    simpa only [unop_comp, Quiver.Hom.unop_op] using hfg _ (Set.mem_op.1 hG) _
  ¬∑ refine Quiver.Hom.unop_inj (hùí¢ _ _ fun G hG h => Quiver.Hom.op_inj ?_)
    simpa only [op_comp, Quiver.Hom.op_unop] using hfg _ (Set.op_mem_op.2 hG) _

theorem isCoseparating_unop_iff (ùí¢ : Set C·µí·µñ) : IsCoseparating ùí¢.unop ‚Üî IsSeparating ùí¢ := by
  rw [‚Üê isSeparating_op_iff, Set.unop_op]

theorem isSeparating_unop_iff (ùí¢ : Set C·µí·µñ) : IsSeparating ùí¢.unop ‚Üî IsCoseparating ùí¢ := by
  rw [‚Üê isCoseparating_op_iff, Set.unop_op]

theorem isDetecting_op_iff (ùí¢ : Set C) : IsDetecting ùí¢.op ‚Üî IsCodetecting ùí¢ := by
  refine ‚ü®fun hùí¢ X Y f hf => ?_, fun hùí¢ X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_op_iff _).1 (hùí¢ _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (unop G) (Set.mem_op.1 hG) h.unop
    exact
      ‚ü®t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hùí¢ _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (op G) (Set.op_mem_op.2 hG) h.op
    refine ‚ü®t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)‚ü©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isCodetecting_op_iff (ùí¢ : Set C) : IsCodetecting ùí¢.op ‚Üî IsDetecting ùí¢ := by
  refine ‚ü®fun hùí¢ X Y f hf => ?_, fun hùí¢ X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_op_iff _).1 (hùí¢ _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (unop G) (Set.mem_op.1 hG) h.unop
    exact
      ‚ü®t.op, Quiver.Hom.unop_inj ht, fun y hy => Quiver.Hom.unop_inj (ht' _ (Quiver.Hom.op_inj hy))‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hùí¢ _ fun G hG h => ?_)
    obtain ‚ü®t, ht, ht'‚ü© := hf (op G) (Set.op_mem_op.2 hG) h.op
    refine ‚ü®t.unop, Quiver.Hom.op_inj ht, fun y hy => Quiver.Hom.op_inj (ht' _ ?_)‚ü©
    exact Quiver.Hom.unop_inj (by simpa only using hy)

theorem isDetecting_unop_iff (ùí¢ : Set C·µí·µñ) : IsDetecting ùí¢.unop ‚Üî IsCodetecting ùí¢ := by
  rw [‚Üê isCodetecting_op_iff, Set.unop_op]

theorem isCodetecting_unop_iff {ùí¢ : Set C·µí·µñ} : IsCodetecting ùí¢.unop ‚Üî IsDetecting ùí¢ := by
  rw [‚Üê isDetecting_op_iff, Set.unop_op]

end Dual

theorem IsDetecting.isSeparating [HasEqualizers C] {ùí¢ : Set C} (hùí¢ : IsDetecting ùí¢) :
    IsSeparating ùí¢ := fun _ _ f g hfg =>
  have : IsIso (equalizer.Œπ f g) := hùí¢ _ fun _ hG _ => equalizer.existsUnique _ (hfg _ hG _)
  eq_of_epi_equalizer

section

theorem IsCodetecting.isCoseparating [HasCoequalizers C] {ùí¢ : Set C} :
    IsCodetecting ùí¢ ‚Üí IsCoseparating ùí¢ := by
  simpa only [‚Üê isSeparating_op_iff, ‚Üê isDetecting_op_iff] using IsDetecting.isSeparating

end

theorem IsSeparating.isDetecting [Balanced C] {ùí¢ : Set C} (hùí¢ : IsSeparating ùí¢) :
    IsDetecting ùí¢ := by
  intro X Y f hf
  refine
    (isIso_iff_mono_and_epi _).2 ‚ü®‚ü®fun g h hgh => hùí¢ _ _ fun G hG i => ?_‚ü©, ‚ü®fun g h hgh => ?_‚ü©‚ü©
  ¬∑ obtain ‚ü®t, -, ht‚ü© := hf G hG (i ‚â´ g ‚â´ f)
    rw [ht (i ‚â´ g) (Category.assoc _ _ _), ht (i ‚â´ h) (hgh.symm ‚ñ∏ Category.assoc _ _ _)]
  ¬∑ refine hùí¢ _ _ fun G hG i => ?_
    obtain ‚ü®t, rfl, -‚ü© := hf G hG i
    rw [Category.assoc, hgh, Category.assoc]

section

attribute [local instance] balanced_opposite

theorem IsCoseparating.isCodetecting [Balanced C] {ùí¢ : Set C} :
    IsCoseparating ùí¢ ‚Üí IsCodetecting ùí¢ := by
  simpa only [‚Üê isDetecting_op_iff, ‚Üê isSeparating_op_iff] using IsSeparating.isDetecting

end

theorem isDetecting_iff_isSeparating [HasEqualizers C] [Balanced C] (ùí¢ : Set C) :
    IsDetecting ùí¢ ‚Üî IsSeparating ùí¢ :=
  ‚ü®IsDetecting.isSeparating, IsSeparating.isDetecting‚ü©

theorem isCodetecting_iff_isCoseparating [HasCoequalizers C] [Balanced C] {ùí¢ : Set C} :
    IsCodetecting ùí¢ ‚Üî IsCoseparating ùí¢ :=
  ‚ü®IsCodetecting.isCoseparating, IsCoseparating.isCodetecting‚ü©

section Mono

theorem IsSeparating.mono {ùí¢ : Set C} (hùí¢ : IsSeparating ùí¢) {‚Ñã : Set C} (hùí¢‚Ñã : ùí¢ ‚äÜ ‚Ñã) :
    IsSeparating ‚Ñã := fun _ _ _ _ hfg => hùí¢ _ _ fun _ hG _ => hfg _ (hùí¢‚Ñã hG) _

theorem IsCoseparating.mono {ùí¢ : Set C} (hùí¢ : IsCoseparating ùí¢) {‚Ñã : Set C} (hùí¢‚Ñã : ùí¢ ‚äÜ ‚Ñã) :
    IsCoseparating ‚Ñã := fun _ _ _ _ hfg => hùí¢ _ _ fun _ hG _ => hfg _ (hùí¢‚Ñã hG) _

theorem IsDetecting.mono {ùí¢ : Set C} (hùí¢ : IsDetecting ùí¢) {‚Ñã : Set C} (hùí¢‚Ñã : ùí¢ ‚äÜ ‚Ñã) :
    IsDetecting ‚Ñã := fun _ _ _ hf => hùí¢ _ fun _ hG _ => hf _ (hùí¢‚Ñã hG) _

theorem IsCodetecting.mono {ùí¢ : Set C} (hùí¢ : IsCodetecting ùí¢) {‚Ñã : Set C} (hùí¢‚Ñã : ùí¢ ‚äÜ ‚Ñã) :
    IsCodetecting ‚Ñã := fun _ _ _ hf => hùí¢ _ fun _ hG _ => hf _ (hùí¢‚Ñã hG) _

end Mono

section Empty

theorem thin_of_isSeparating_empty (h : IsSeparating (‚àÖ : Set C)) : Quiver.IsThin C := fun _ _ =>
  ‚ü®fun _ _ => h _ _ fun _ => False.elim‚ü©

theorem isSeparating_empty_of_thin [Quiver.IsThin C] : IsSeparating (‚àÖ : Set C) :=
  fun _ _ _ _ _ => Subsingleton.elim _ _

theorem thin_of_isCoseparating_empty (h : IsCoseparating (‚àÖ : Set C)) : Quiver.IsThin C :=
  fun _ _ => ‚ü®fun _ _ => h _ _ fun _ => False.elim‚ü©

theorem isCoseparating_empty_of_thin [Quiver.IsThin C] : IsCoseparating (‚àÖ : Set C) :=
  fun _ _ _ _ _ => Subsingleton.elim _ _

theorem groupoid_of_isDetecting_empty (h : IsDetecting (‚àÖ : Set C)) {X Y : C} (f : X ‚ü∂ Y) :
    IsIso f :=
  h _ fun _ => False.elim

theorem isDetecting_empty_of_groupoid [‚àÄ {X Y : C} (f : X ‚ü∂ Y), IsIso f] :
    IsDetecting (‚àÖ : Set C) := fun _ _ _ _ => inferInstance

theorem groupoid_of_isCodetecting_empty (h : IsCodetecting (‚àÖ : Set C)) {X Y : C} (f : X ‚ü∂ Y) :
    IsIso f :=
  h _ fun _ => False.elim

theorem isCodetecting_empty_of_groupoid [‚àÄ {X Y : C} (f : X ‚ü∂ Y), IsIso f] :
    IsCodetecting (‚àÖ : Set C) := fun _ _ _ _ => inferInstance

end Empty

theorem isSeparating_iff_epi (ùí¢ : Set C)
    [‚àÄ A : C, HasCoproduct fun f : Œ£G : ùí¢, (G : C) ‚ü∂ A => (f.1 : C)] :
    IsSeparating ùí¢ ‚Üî ‚àÄ A : C, Epi (Sigma.desc (@Sigma.snd ùí¢ fun G => (G : C) ‚ü∂ A)) := by
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun G hG f => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using Sigma.Œπ (fun f : Œ£G : ùí¢, (G : C) ‚ü∂ A => (f.1 : C)) ‚ü®‚ü®G, hG‚ü©, f‚ü© ‚â´= huv
  ¬∑ haveI := h X
    refine
      (cancel_epi (Sigma.desc (@Sigma.snd ùí¢ fun G => (G : C) ‚ü∂ X))).1 (colimit.hom_ext fun j => ?_)
    simpa using hh j.as.1.1 j.as.1.2 j.as.2

theorem isCoseparating_iff_mono (ùí¢ : Set C)
    [‚àÄ A : C, HasProduct fun f : Œ£G : ùí¢, A ‚ü∂ (G : C) => (f.1 : C)] :
    IsCoseparating ùí¢ ‚Üî ‚àÄ A : C, Mono (Pi.lift (@Sigma.snd ùí¢ fun G => A ‚ü∂ (G : C))) := by
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun G hG f => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using huv =‚â´ Pi.œÄ (fun f : Œ£G : ùí¢, A ‚ü∂ (G : C) => (f.1 : C)) ‚ü®‚ü®G, hG‚ü©, f‚ü©
  ¬∑ haveI := h Y
    refine (cancel_mono (Pi.lift (@Sigma.snd ùí¢ fun G => Y ‚ü∂ (G : C)))).1 (limit.hom_ext fun j => ?_)
    simpa using hh j.as.1.1 j.as.1.2 j.as.2

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a complete well-powered
    category with a small coseparating set has an initial object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already cocomplete,
    see `hasColimits_of_hasLimits_of_isCoseparating`. -/
theorem hasInitial_of_isCoseparating [WellPowered C] [HasLimits C] {ùí¢ : Set C} [Small.{v‚ÇÅ} ùí¢]
    (hùí¢ : IsCoseparating ùí¢) : HasInitial C := by
  haveI : HasProductsOfShape ùí¢ C := hasProductsOfShape_of_small C ùí¢
  haveI := fun A => hasProductsOfShape_of_small.{v‚ÇÅ} C (Œ£G : ùí¢, A ‚ü∂ (G : C))
  letI := completeLatticeOfCompleteSemilatticeInf (Subobject (piObj (Subtype.val : ùí¢ ‚Üí C)))
  suffices ‚àÄ A : C, Unique (((‚ä• : Subobject (piObj (Subtype.val : ùí¢ ‚Üí C))) : C) ‚ü∂ A) by
    exact hasInitial_of_unique ((‚ä• : Subobject (piObj (Subtype.val : ùí¢ ‚Üí C))) : C)
  refine fun A => ‚ü®‚ü®?_‚ü©, fun f => ?_‚ü©
  ¬∑ let s := Pi.lift fun f : Œ£G : ùí¢, A ‚ü∂ (G : C) => id (Pi.œÄ (Subtype.val : ùí¢ ‚Üí C)) f.1
    let t := Pi.lift (@Sigma.snd ùí¢ fun G => A ‚ü∂ (G : C))
    haveI : Mono t := (isCoseparating_iff_mono ùí¢).1 hùí¢ A
    exact Subobject.ofLEMk _ (pullback.fst _ _ : pullback s t ‚ü∂ _) bot_le ‚â´ pullback.snd _ _
  ¬∑ suffices ‚àÄ (g : Subobject.underlying.obj ‚ä• ‚ü∂ A), f = g by
      apply this
    intro g
    suffices IsSplitEpi (equalizer.Œπ f g) by exact eq_of_epi_equalizer
    exact IsSplitEpi.mk' ‚ü®Subobject.ofLEMk _ (equalizer.Œπ f g ‚â´ Subobject.arrow _) bot_le, by
      ext
      simp‚ü©

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a cocomplete well-copowered
    category with a small separating set has a terminal object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already complete, see
    `hasLimits_of_hasColimits_of_isSeparating`. -/
theorem hasTerminal_of_isSeparating [WellPowered C·µí·µñ] [HasColimits C] {ùí¢ : Set C} [Small.{v‚ÇÅ} ùí¢]
    (hùí¢ : IsSeparating ùí¢) : HasTerminal C := by
  haveI : Small.{v‚ÇÅ} ùí¢.op := small_of_injective (Set.opEquiv_self ùí¢).injective
  haveI : HasInitial C·µí·µñ := hasInitial_of_isCoseparating ((isCoseparating_op_iff _).2 hùí¢)
  exact hasTerminal_of_hasInitial_op

section WellPowered

namespace Subobject

theorem eq_of_le_of_isDetecting {ùí¢ : Set C} (hùí¢ : IsDetecting ùí¢) {X : C} (P Q : Subobject X)
    (h‚ÇÅ : P ‚â§ Q) (h‚ÇÇ : ‚àÄ G ‚àà ùí¢, ‚àÄ {f : G ‚ü∂ X}, Q.Factors f ‚Üí P.Factors f) : P = Q := by
  suffices IsIso (ofLE _ _ h‚ÇÅ) by exact le_antisymm h‚ÇÅ (le_of_comm (inv (ofLE _ _ h‚ÇÅ)) (by simp))
  refine hùí¢ _ fun G hG f => ?_
  have : P.Factors (f ‚â´ Q.arrow) := h‚ÇÇ _ hG ((factors_iff _ _).2 ‚ü®_, rfl‚ü©)
  refine ‚ü®factorThru _ _ this, ?_, fun g (hg : g ‚â´ _ = f) => ?_‚ü©
  ¬∑ simp only [‚Üê cancel_mono Q.arrow, Category.assoc, ofLE_arrow, factorThru_arrow]
  ¬∑ simp only [‚Üê cancel_mono (Subobject.ofLE _ _ h‚ÇÅ), ‚Üê cancel_mono Q.arrow, hg, Category.assoc,
      ofLE_arrow, factorThru_arrow]

theorem inf_eq_of_isDetecting [HasPullbacks C] {ùí¢ : Set C} (hùí¢ : IsDetecting ùí¢) {X : C}
    (P Q : Subobject X) (h : ‚àÄ G ‚àà ùí¢, ‚àÄ {f : G ‚ü∂ X}, P.Factors f ‚Üí Q.Factors f) : P ‚äì Q = P :=
  eq_of_le_of_isDetecting hùí¢ _ _ _root_.inf_le_left
    fun _ hG _ hf => (inf_factors _).2 ‚ü®hf, h _ hG hf‚ü©

theorem eq_of_isDetecting [HasPullbacks C] {ùí¢ : Set C} (hùí¢ : IsDetecting ùí¢) {X : C}
    (P Q : Subobject X) (h : ‚àÄ G ‚àà ùí¢, ‚àÄ {f : G ‚ü∂ X}, P.Factors f ‚Üî Q.Factors f) : P = Q :=
  calc
    P = P ‚äì Q := Eq.symm <| inf_eq_of_isDetecting hùí¢ _ _ fun G hG _ hf => (h G hG).1 hf
    _ = Q ‚äì P := inf_comm ..
    _ = Q := inf_eq_of_isDetecting hùí¢ _ _ fun G hG _ hf => (h G hG).2 hf

end Subobject

/-- A category with pullbacks and a small detecting set is well-powered. -/
theorem wellPowered_of_isDetecting [HasPullbacks C] {ùí¢ : Set C} [Small.{v‚ÇÅ} ùí¢]
    (hùí¢ : IsDetecting ùí¢) : WellPowered C :=
  ‚ü®fun X =>
    @small_of_injective _ _ _ (fun P : Subobject X => { f : Œ£G : ùí¢, G.1 ‚ü∂ X | P.Factors f.2 })
      fun P Q h => Subobject.eq_of_isDetecting hùí¢ _ _
        (by simpa [Set.ext_iff, Sigma.forall] using h)‚ü©

end WellPowered

namespace StructuredArrow

variable (S : D) (T : C ‚•§ D)

theorem isCoseparating_proj_preimage {ùí¢ : Set C} (hùí¢ : IsCoseparating ùí¢) :
    IsCoseparating ((proj S T).obj ‚Åª¬π' ùí¢) := by
  refine fun X Y f g hfg => ext _ _ (hùí¢ _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.right (hfg (mk (Y.hom ‚â´ T.map h)) hG (homMk h rfl))

end StructuredArrow

namespace CostructuredArrow

variable (S : C ‚•§ D) (T : D)

theorem isSeparating_proj_preimage {ùí¢ : Set C} (hùí¢ : IsSeparating ùí¢) :
    IsSeparating ((proj S T).obj ‚Åª¬π' ùí¢) := by
  refine fun X Y f g hfg => ext _ _ (hùí¢ _ _ fun G hG h => ?_)
  exact congr_arg CommaMorphism.left (hfg (mk (S.map h ‚â´ X.hom)) hG (homMk h rfl))

end CostructuredArrow

/-- We say that `G` is a separator if the functor `C(G, -)` is faithful. -/
def IsSeparator (G : C) : Prop :=
  IsSeparating ({G} : Set C)

/-- We say that `G` is a coseparator if the functor `C(-, G)` is faithful. -/
def IsCoseparator (G : C) : Prop :=
  IsCoseparating ({G} : Set C)

/-- We say that `G` is a detector if the functor `C(G, -)` reflects isomorphisms. -/
def IsDetector (G : C) : Prop :=
  IsDetecting ({G} : Set C)

/-- We say that `G` is a codetector if the functor `C(-, G)` reflects isomorphisms. -/
def IsCodetector (G : C) : Prop :=
  IsCodetecting ({G} : Set C)

section Dual

theorem isSeparator_op_iff (G : C) : IsSeparator (op G) ‚Üî IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isSeparating_op_iff, Set.singleton_op]

theorem isCoseparator_op_iff (G : C) : IsCoseparator (op G) ‚Üî IsSeparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isCoseparating_op_iff, Set.singleton_op]

theorem isCoseparator_unop_iff (G : C·µí·µñ) : IsCoseparator (unop G) ‚Üî IsSeparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isCoseparating_unop_iff, Set.singleton_unop]

theorem isSeparator_unop_iff (G : C·µí·µñ) : IsSeparator (unop G) ‚Üî IsCoseparator G := by
  rw [IsSeparator, IsCoseparator, ‚Üê isSeparating_unop_iff, Set.singleton_unop]

theorem isDetector_op_iff (G : C) : IsDetector (op G) ‚Üî IsCodetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isDetecting_op_iff, Set.singleton_op]

theorem isCodetector_op_iff (G : C) : IsCodetector (op G) ‚Üî IsDetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isCodetecting_op_iff, Set.singleton_op]

theorem isCodetector_unop_iff (G : C·µí·µñ) : IsCodetector (unop G) ‚Üî IsDetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isCodetecting_unop_iff, Set.singleton_unop]

theorem isDetector_unop_iff (G : C·µí·µñ) : IsDetector (unop G) ‚Üî IsCodetector G := by
  rw [IsDetector, IsCodetector, ‚Üê isDetecting_unop_iff, Set.singleton_unop]

end Dual

theorem IsDetector.isSeparator [HasEqualizers C] {G : C} : IsDetector G ‚Üí IsSeparator G :=
  IsDetecting.isSeparating

theorem IsCodetector.isCoseparator [HasCoequalizers C] {G : C} : IsCodetector G ‚Üí IsCoseparator G :=
  IsCodetecting.isCoseparating

theorem IsSeparator.isDetector [Balanced C] {G : C} : IsSeparator G ‚Üí IsDetector G :=
  IsSeparating.isDetecting

theorem IsCoseparator.isCodetector [Balanced C] {G : C} : IsCoseparator G ‚Üí IsCodetector G :=
  IsCoseparating.isCodetecting

theorem isSeparator_def (G : C) :
    IsSeparator G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ X, h ‚â´ f = h ‚â´ g) ‚Üí f = g :=
  ‚ü®fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (Set.mem_singleton _) _‚ü©

theorem IsSeparator.def {G : C} :
    IsSeparator G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ X, h ‚â´ f = h ‚â´ g) ‚Üí f = g :=
  (isSeparator_def _).1

theorem isCoseparator_def (G : C) :
    IsCoseparator G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : Y ‚ü∂ G, f ‚â´ h = g ‚â´ h) ‚Üí f = g :=
  ‚ü®fun hG X Y f g hfg =>
    hG _ _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hfg h,
    fun hG _ _ _ _ hfg => hG _ _ fun _ => hfg _ (Set.mem_singleton _) _‚ü©

theorem IsCoseparator.def {G : C} :
    IsCoseparator G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), (‚àÄ h : Y ‚ü∂ G, f ‚â´ h = g ‚â´ h) ‚Üí f = g :=
  (isCoseparator_def _).1

theorem isDetector_def (G : C) :
    IsDetector G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ Y, ‚àÉ! h', h' ‚â´ f = h) ‚Üí IsIso f :=
  ‚ü®fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (Set.mem_singleton _) _‚ü©

theorem IsDetector.def {G : C} :
    IsDetector G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : G ‚ü∂ Y, ‚àÉ! h', h' ‚â´ f = h) ‚Üí IsIso f :=
  (isDetector_def _).1

theorem isCodetector_def (G : C) :
    IsCodetector G ‚Üî ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : X ‚ü∂ G, ‚àÉ! h', f ‚â´ h' = h) ‚Üí IsIso f :=
  ‚ü®fun hG X Y f hf =>
    hG _ fun H hH h => by
      obtain rfl := Set.mem_singleton_iff.1 hH
      exact hf h,
    fun hG _ _ _ hf => hG _ fun _ => hf _ (Set.mem_singleton _) _‚ü©

theorem IsCodetector.def {G : C} :
    IsCodetector G ‚Üí ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), (‚àÄ h : X ‚ü∂ G, ‚àÉ! h', f ‚â´ h' = h) ‚Üí IsIso f :=
  (isCodetector_def _).1

theorem isSeparator_iff_faithful_coyoneda_obj (G : C) :
    IsSeparator G ‚Üî (coyoneda.obj (op G)).Faithful :=
  ‚ü®fun hG => ‚ü®fun hfg => hG.def _ _ (congr_fun hfg)‚ü©, fun _ =>
    (isSeparator_def _).2 fun _ _ _ _ hfg => (coyoneda.obj (op G)).map_injective (funext hfg)‚ü©

theorem isCoseparator_iff_faithful_yoneda_obj (G : C) : IsCoseparator G ‚Üî (yoneda.obj G).Faithful :=
  ‚ü®fun hG => ‚ü®fun hfg => Quiver.Hom.unop_inj (hG.def _ _ (congr_fun hfg))‚ü©, fun _ =>
    (isCoseparator_def _).2 fun _ _ _ _ hfg =>
      Quiver.Hom.op_inj <| (yoneda.obj G).map_injective (funext hfg)‚ü©

theorem isSeparator_iff_epi (G : C) [‚àÄ A : C, HasCoproduct fun _ : G ‚ü∂ A => G] :
    IsSeparator G ‚Üî ‚àÄ A : C, Epi (Sigma.desc fun f : G ‚ü∂ A => f) := by
  rw [isSeparator_def]
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun i => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using Sigma.Œπ _ i ‚â´= huv
  ¬∑ haveI := h X
    refine (cancel_epi (Sigma.desc fun f : G ‚ü∂ X => f)).1 (colimit.hom_ext fun j => ?_)
    simpa using hh j.as

theorem isCoseparator_iff_mono (G : C) [‚àÄ A : C, HasProduct fun _ : A ‚ü∂ G => G] :
    IsCoseparator G ‚Üî ‚àÄ A : C, Mono (Pi.lift fun f : A ‚ü∂ G => f) := by
  rw [isCoseparator_def]
  refine ‚ü®fun h A => ‚ü®fun u v huv => h _ _ fun i => ?_‚ü©, fun h X Y f g hh => ?_‚ü©
  ¬∑ simpa using huv =‚â´ Pi.œÄ _ i
  ¬∑ haveI := h Y
    refine (cancel_mono (Pi.lift fun f : Y ‚ü∂ G => f)).1 (limit.hom_ext fun j => ?_)
    simpa using hh j.as

section ZeroMorphisms

variable [HasZeroMorphisms C]

theorem isSeparator_coprod (G H : C) [HasBinaryCoproduct G H] :
    IsSeparator (G ‚®ø H) ‚Üî IsSeparating ({G, H} : Set C) := by
  refine
    ‚ü®fun h X Y u v huv => ?_, fun h =>
      (isSeparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_‚ü©
  ¬∑ refine h.def _ _ fun g => coprod.hom_ext ?_ ?_
    ¬∑ simpa using huv G (by simp) (coprod.inl ‚â´ g)
    ¬∑ simpa using huv H (by simp) (coprod.inr ‚â´ g)
  ¬∑ simp only [Set.mem_insert_iff, Set.mem_singleton_iff] at hZ
    rcases hZ with (rfl | rfl)
    ¬∑ simpa using coprod.inl ‚â´= huv (coprod.desc g 0)
    ¬∑ simpa using coprod.inr ‚â´= huv (coprod.desc 0 g)

theorem isSeparator_coprod_of_isSeparator_left (G H : C) [HasBinaryCoproduct G H]
    (hG : IsSeparator G) : IsSeparator (G ‚®ø H) :=
  (isSeparator_coprod _ _).2 <| IsSeparating.mono hG <| by simp

theorem isSeparator_coprod_of_isSeparator_right (G H : C) [HasBinaryCoproduct G H]
    (hH : IsSeparator H) : IsSeparator (G ‚®ø H) :=
  (isSeparator_coprod _ _).2 <| IsSeparating.mono hH <| by simp

theorem isSeparator_sigma {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasCoproduct f] :
    IsSeparator (‚àê f) ‚Üî IsSeparating (Set.range f) := by
  refine
    ‚ü®fun h X Y u v huv => ?_, fun h =>
      (isSeparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_‚ü©
  ¬∑ refine h.def _ _ fun g => colimit.hom_ext fun b => ?_
    simpa using huv (f b.as) (by simp) (colimit.Œπ (Discrete.functor f) _ ‚â´ g)
  ¬∑ obtain ‚ü®b, rfl‚ü© := Set.mem_range.1 hZ
    classical simpa using Sigma.Œπ f b ‚â´= huv (Sigma.desc (Pi.single b g))

theorem isSeparator_sigma_of_isSeparator {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasCoproduct f] (b : Œ≤)
    (hb : IsSeparator (f b)) : IsSeparator (‚àê f) :=
  (isSeparator_sigma _).2 <| IsSeparating.mono hb <| by simp

theorem isCoseparator_prod (G H : C) [HasBinaryProduct G H] :
    IsCoseparator (G ‚®Ø H) ‚Üî IsCoseparating ({G, H} : Set C) := by
  refine
    ‚ü®fun h X Y u v huv => ?_, fun h =>
      (isCoseparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_‚ü©
  ¬∑ refine h.def _ _ fun g => Limits.prod.hom_ext ?_ ?_
    ¬∑ simpa using huv G (by simp) (g ‚â´ Limits.prod.fst)
    ¬∑ simpa using huv H (by simp) (g ‚â´ Limits.prod.snd)
  ¬∑ simp only [Set.mem_insert_iff, Set.mem_singleton_iff] at hZ
    rcases hZ with (rfl | rfl)
    ¬∑ simpa using huv (prod.lift g 0) =‚â´ Limits.prod.fst
    ¬∑ simpa using huv (prod.lift 0 g) =‚â´ Limits.prod.snd

theorem isCoseparator_prod_of_isCoseparator_left (G H : C) [HasBinaryProduct G H]
    (hG : IsCoseparator G) : IsCoseparator (G ‚®Ø H) :=
  (isCoseparator_prod _ _).2 <| IsCoseparating.mono hG <| by simp

theorem isCoseparator_prod_of_isCoseparator_right (G H : C) [HasBinaryProduct G H]
    (hH : IsCoseparator H) : IsCoseparator (G ‚®Ø H) :=
  (isCoseparator_prod _ _).2 <| IsCoseparating.mono hH <| by simp

theorem isCoseparator_pi {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasProduct f] :
    IsCoseparator (‚àè·∂ú f) ‚Üî IsCoseparating (Set.range f) := by
  refine
    ‚ü®fun h X Y u v huv => ?_, fun h =>
      (isCoseparator_def _).2 fun X Y u v huv => h _ _ fun Z hZ g => ?_‚ü©
  ¬∑ refine h.def _ _ fun g => limit.hom_ext fun b => ?_
    simpa using huv (f b.as) (by simp) (g ‚â´ limit.œÄ (Discrete.functor f) _)
  ¬∑ obtain ‚ü®b, rfl‚ü© := Set.mem_range.1 hZ
    classical simpa using huv (Pi.lift (Pi.single b g)) =‚â´ Pi.œÄ f b

theorem isCoseparator_pi_of_isCoseparator {Œ≤ : Type w} (f : Œ≤ ‚Üí C) [HasProduct f] (b : Œ≤)
    (hb : IsCoseparator (f b)) : IsCoseparator (‚àè·∂ú f) :=
  (isCoseparator_pi _).2 <| IsCoseparating.mono hb <| by simp

end ZeroMorphisms

theorem isDetector_iff_reflectsIsomorphisms_coyoneda_obj (G : C) :
    IsDetector G ‚Üî (coyoneda.obj (op G)).ReflectsIsomorphisms := by
  refine
    ‚ü®fun hG => ‚ü®fun f hf => hG.def _ fun h => ?_‚ü©, fun h =>
      (isDetector_def _).2 fun X Y f hf => ?_‚ü©
  ¬∑ rw [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
    exact hf h
  ¬∑ suffices IsIso ((coyoneda.obj (op G)).map f) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (coyoneda.obj (op G)) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem isCodetector_iff_reflectsIsomorphisms_yoneda_obj (G : C) :
    IsCodetector G ‚Üî (yoneda.obj G).ReflectsIsomorphisms := by
  refine ‚ü®fun hG => ‚ü®fun f hf => ?_‚ü©, fun h => (isCodetector_def _).2 fun X Y f hf => ?_‚ü©
  ¬∑ refine (isIso_unop_iff _).1 (hG.def _ ?_)
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique] at hf
  ¬∑ rw [‚Üê isIso_op_iff]
    suffices IsIso ((yoneda.obj G).map f.op) by
      exact @isIso_of_reflects_iso _ _ _ _ _ _ _ (yoneda.obj G) _ h
    rwa [isIso_iff_bijective, Function.bijective_iff_existsUnique]

theorem wellPowered_of_isDetector [HasPullbacks C] (G : C) (hG : IsDetector G) : WellPowered C :=
  -- Porting note: added the following `haveI` to prevent universe issues
  haveI := small_subsingleton ({G} : Set C)
  wellPowered_of_isDetecting hG

section HasGenerator

class HasSeparator : Prop :=
  hasSeparator : ‚àÉ G : E, IsSeparator G

class HasCoseparator : Prop :=
  hasCoseparator : ‚àÉ G : E, IsCoseparator G

class HasDetector : Prop :=
  hasDetector : ‚àÉ G : E, IsDetector G

class HasCodetector : Prop :=
  hasCodetector : ‚àÉ G : E, IsCodetector G

theorem HasSeparator.hasDetector [Balanced E] [HasSeparator E] : HasDetector E := by
  obtain ‚ü®G, hG‚ü© : HasSeparator E := inferInstance
  exact ‚ü®G, hG.isDetector‚ü©

theorem HasDetector.hasSeparator [HasEqualizers E] [HasDetector E] : HasSeparator E := by
  obtain ‚ü®G, hG‚ü© : HasDetector E := inferInstance
  exact ‚ü®G, hG.isSeparator‚ü©

theorem HasCoseparator.hasCodetector [Balanced E] [HasCoseparator E] : HasCodetector E := by
  obtain ‚ü®G, hG‚ü© : HasCoseparator E := inferInstance
  exact ‚ü®G, hG.isCodetector‚ü©

theorem HasCodetector.hasCoseparator [HasCoequalizers E] [HasCodetector E] : HasCoseparator E := by
  obtain ‚ü®G, hG‚ü© : HasCodetector E := inferInstance
  exact ‚ü®G, hG.isCoseparator‚ü©

theorem HasDetector.wellPowered [HasPullbacks E] [HasDetector E] : WellPowered E := by
  obtain ‚ü®G, hG‚ü© : HasDetector E := inferInstance
  exact wellPowered_of_isDetector G hG

theorem HasSeparator.wellPowered [HasPullbacks E] [Balanced E] [HasSeparator E] :
    WellPowered E := (HasSeparator.hasDetector E).wellPowered

end HasGenerator

section Dual

theorem HasSeparator.hasCoseparator_op [HasSeparator C] : HasCoseparator C·µí·µñ := by
  obtain ‚ü®G, hG‚ü© : HasSeparator C := inferInstance
  exact ‚ü®op G, (isCoseparator_op_iff G).mpr hG‚ü©

theorem HasSeparator.hasCoseparator_unop [HasSeparator C·µí·µñ] : HasCoseparator C := by
  obtain ‚ü®G, hG‚ü© : HasSeparator C·µí·µñ := inferInstance
  exact ‚ü®unop G, (isCoseparator_unop_iff G).mpr hG‚ü©

theorem HasCoseparator.hasSeparator_op [HasCoseparator C] : HasSeparator C·µí·µñ := by
  obtain ‚ü®G, hG‚ü© : HasCoseparator C := inferInstance
  exact ‚ü®op G, (isSeparator_op_iff G).mpr hG‚ü©

theorem HasCoseparator.hasSeparator_unop [HasCoseparator C·µí·µñ] : HasSeparator C := by
  obtain ‚ü®G, hG‚ü© : HasCoseparator C·µí·µñ := inferInstance
  exact ‚ü®unop G, (isSeparator_unop_iff G).mpr hG‚ü©

theorem HasDetector.hasCodetector_op [HasDetector C] : HasCodetector C·µí·µñ := by
  obtain ‚ü®G, hG‚ü© : HasDetector C := inferInstance
  exact ‚ü®op G, (isCodetector_op_iff G).mpr hG‚ü©

theorem HasDetector.hasCoDetector_unop [HasDetector C·µí·µñ] : HasCodetector C := by
  obtain ‚ü®G, hG‚ü© : HasDetector C·µí·µñ := inferInstance
  exact ‚ü®unop G, (isCodetector_unop_iff G).mpr hG‚ü©

theorem HasCodetector.hasDetector_op [HasCodetector C] : HasDetector C·µí·µñ := by
  obtain ‚ü®G, hG‚ü© : HasCodetector C := inferInstance
  exact ‚ü®op G, (isDetector_op_iff G).mpr hG‚ü©

theorem HasCodetector.hasDetector_unop [HasCodetector C·µí·µñ] : HasDetector C := by
  obtain ‚ü®G, hG‚ü© : HasCodetector C·µí·µñ := inferInstance
  exact ‚ü®unop G, (isDetector_unop_iff G).mpr hG‚ü©

end Dual

end CategoryTheory
